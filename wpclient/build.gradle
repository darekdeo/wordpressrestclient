apply plugin: 'groovy'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = "0.2.3"
group = "gq.coderetort.wpclient"

def retrofitLibVersion = '2.3.0'
def okhttpLibVersion = '3.6.0'
def groovyLibVersion = '2.4.10'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
    testImplementation 'org.json:json:20170516'
    testImplementation 'junit:junit:4.12'

    compile "org.codehaus.groovy:groovy:$groovyLibVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitLibVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitLibVersion"
    compile "com.squareup.okhttp3:okhttp:$okhttpLibVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpLibVersion"
}

sourceSets {
    groovydoc {
        resources {
            srcDir 'src/groovydoc'
        }
    }
}

groovydoc {
    classpath = sourceSets.groovydoc.output + classpath
    includes  = ["**/dsl/**"]
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def siteUrl = 'https://github.com/darekdeo/wordpressrestclient'
def gitUrl = 'https://github.com/darekdeo/wordpressrestclient.git'
def libraryDescription = 'WordPress rest client library.'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'WordPress rest client'
                description libraryDescription
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'darekdeo'
                        name 'Dariusz Deoniziak'
                        email 'darekdeoniziak@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publish = true
    pkg {
        repo = "wordpressrestclient"
        name = "wprestclient"
        websiteUrl = siteUrl
        issueTrackerUrl = "$siteUrl/issues"
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}